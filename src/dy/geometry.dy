import std.algorithm.comparison
import std.math
import std.traits


private template _coordOps()
    nothrow pure @safe @nogc:

    auto ref opOpAssign(string op)(auto ref const typeof(this) rhs) if (op.among!("+", "-"))
        foreach (field; FieldNameTuple!(typeof(this)))
            mixin(field ~ op ~ `= rhs.` ~ field ~ `;`)
        return this

    auto opBinary(string op)(auto ref const typeof(this) rhs) const if (op.among!("+", "-"))
        Unqual!(typeof(this)) temp = this
        return mixin(`temp ` ~ op ~ `= rhs`)


struct RectCoord
    int y, x

    mixin _coordOps

    @property int dist() const nothrow pure @safe @nogc \
    out (result)
        assert(result >= 0)
    do
        return abs(y) + abs(x)


struct HexCoord
    nothrow pure @safe @nogc:

    int skew, horiz

    static HexCoord fromRect(double y, double x, double scale)
        const t = y * (1. / 3. / scale)
        const skew = t * 2
        const horiz = x * (1. / sqrt(3.) / scale) - t
        return HexCoord(cast(int)lrint(skew), cast(int)lrint(horiz))

    mixin _coordOps

    @property int dist() const \
    out (result)
        assert(result >= 0)
    do
        int result = abs(skew + horiz)
        if ((skew ^ horiz) < 0)
            result += min(abs(skew), abs(horiz))
        return result
